apply plugin: "java"

sourceCompatibility = targetCompatibility = 1.8

/* define the properties file */
ext.configFile = file "build.properties"

configFile.withReader {
    /* read config.  it shall from now on be referenced as simply config or as project.config */
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

version = config.plugin_version

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

ext {
    /* the build number that this plugin is made for */
    mindustryVersion = config.mindustry_version
}

dependencies{
    compileOnly "com.github.Anuken.Arc:arc-core:$mindustryVersion"
    compileOnly "com.github.Anuken.Mindustry:core:$mindustryVersion"
}

jar {
    version = null
    from {
        configurations.runtimeClasspath.collect{it.isDirectory() ? it : zipTree(it)}
    }
}

processResources {
    /* this will ensure that this task is redone when the versions change. */
    inputs.property "version", project.version

    filesMatching('plugin.json') {
        expand ([
            'version': project.version
        ])
    }
}
